apply plugin: 'com.android.application'

def consumerKey, consumerSecret, accessToken, accessTokenSecret

if (System.getenv("CONSUMER_KEY") != null) {
    println("### Environment variables are found. ###")
    consumerKey = System.getenv("CONSUMER_KEY")
    consumerSecret = System.getenv("CONSUMER_SECRET")
    accessToken = System.getenv("ACCESS_TOKEN")
    accessTokenSecret = System.getenv("ACCESS_TOKEN_SECRET")
} else {
    Properties props = new Properties()
    try {
        props.load(file('./tokens.properties').newDataInputStream())
        consumerKey = props.get("consumerKey")
        consumerSecret = props.get("consumerSecret")
        accessToken = props.get("accessToken")
        accessTokenSecret = props.get("accessTokenSecret")
    } catch (Exception ignored) {
        throw new GradleException("Missing tokens.properties, check the tokens.properties file.");
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "net.lacolaco.smileessence"
        minSdkVersion 17
        maxSdkVersion 23
        targetSdkVersion 23
        versionCode 26
        versionName "1.0.12"
        buildConfigField "String", "CONSUMER_KEY", "\"${consumerKey}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${consumerSecret}\""
        buildConfigField "String", "ACCESS_TOKEN", "\"${accessToken}\""
        buildConfigField "String", "ACCESS_TOKEN_SECRET", "\"${accessTokenSecret}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:support-v13:23.1.1'
    // crouton
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // exclusion is not necessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }
    // pull to refresh
    compile 'com.navercorp.pulltorefresh:library:3.2.0@aar'
    // t4j
    compile 'org.twitter4j:twitter4j-core:4.0.6'
    compile 'org.twitter4j:twitter4j-media-support:4.0.6'
    compile 'org.twitter4j:twitter4j-stream:4.0.6'
    // twitter-text
    compile 'com.twitter:twitter-text:1.6.1'
    // guava
    compile 'com.google.guava:guava:18.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
}
